<?xml version="1.0"?>
<!-- This is XSLT stylesheet for converting Premis XML reports to more simplified HTML version.
     Usage: xsltproc stylesheet.xml report.xml  > report.html -->

<xsl:stylesheet version="1.0"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:premis="info:lc/xmlns/premis-v2">

    <xsl:template match="/">
        <html lang="en">
            <head>
                <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
                <style>
                    <xsl:value-of select="document('css/bootstrap.min.css')" disable-output-escaping="yes" />
                    <xsl:value-of select="document('css/bootstrap.css')" disable-output-escaping="yes" />
                    <xsl:value-of select="document('css/report.css')" disable-output-escaping="yes" />
                </style>

                <title>Digital preservation service – Ingest report</title>
            </head>
            <body>
                <div id="row">
                    <xsl:choose>
                        <xsl:when test="((/premis:premis/premis:event/premis:eventOutcomeInformation/premis:eventOutcome = 'failure') and not (/premis:premis/premis:event/premis:eventType = 'approval'))">
                            <h1>Ingest report – <span class="text-red">Submission rejected</span></h1>
                        </xsl:when>
                        <xsl:otherwise>
                            <h1>Ingest report – <span class="text-success">Submission accepted</span></h1>
                        </xsl:otherwise>
                    </xsl:choose>
                </div>

                <div id="row">
                    <hr class="divider"/>
                </div>

                <xsl:choose>
                    <xsl:when test="((/premis:premis/premis:event/premis:eventOutcomeInformation/premis:eventOutcome = 'failure') and not (/premis:premis/premis:event/premis:eventType = 'approval'))">
                        <xsl:variable name="objid" select="//premis:object/premis:environment/premis:dependency/premis:dependencyIdentifier/premis:dependencyIdentifierType[text() = 'mets:OBJID']/../premis:dependencyIdentifierValue/text()"/>
                        <xsl:variable name="username" select="//premis:agent/premis:agentIdentifier/premis:agentIdentifierType[text() = 'preservation-user-id']/../../premis:agentName/text()"/>

                        <p class="lead">Digital preservation service has received submission information package <strong><xsl:value-of select="$objid"/></strong> from user <strong><xsl:value-of select="$username"/></strong> and performed following actions. Some actions have failed and preservation responsibility has not been changed to the digital preservation service.
                        </p>
                    </xsl:when>
                    <xsl:otherwise>
                        <xsl:variable name="objid" select="//premis:object/premis:environment/premis:dependency/premis:dependencyIdentifier/premis:dependencyIdentifierType[text() = 'mets:OBJID']/../premis:dependencyIdentifierValue/text()"/>
                        <xsl:variable name="username" select="//premis:agent/premis:agentIdentifier/premis:agentIdentifierType[text() = 'preservation-user-id']/../../premis:agentName/text()"/>

                        <p class="lead">Digital preservation service has received submission information package <strong><xsl:value-of select="$objid"/></strong> from user <strong><xsl:value-of select="$username"/></strong> and performed following actions. All actions were successful and preservation responsibility has been changed to the digital preservation service.
                        </p>
                    </xsl:otherwise>
                </xsl:choose>

                <xsl:for-each select="//premis:object">
                    <xsl:variable name="object_id_type" select="premis:objectIdentifier/premis:objectIdentifierType"/>
                    <xsl:variable name="object_id_value" select="premis:objectIdentifier/premis:objectIdentifierValue"/>

                    <xsl:if test="$object_id_type = 'preservation-sip-id'">
                        <h2>Submission information package <small><xsl:value-of select="premis:originalName"/></small></h2>
                    </xsl:if>
                    <xsl:if test="$object_id_type = 'preservation-signature-id'">
                        <h2>Digital signature <small><xsl:value-of select="premis:originalName"/></small></h2>
                    </xsl:if>
                    <xsl:if test="$object_id_type = 'preservation-mets-id'">
                        <h2>Metadata object <small><xsl:value-of select="premis:originalName"/></small></h2>
                    </xsl:if>
                    <xsl:if test="$object_id_type = 'preservation-object-id'">
                        <h2>Digital object <small><xsl:value-of select="premis:originalName"/></small></h2>
                    </xsl:if>
                    <xsl:if test="($object_id_type = 'preservation-aip-id') and not (/premis:premis/premis:event/premis:eventOutcomeInformation/premis:eventOutcome = 'failure')">
                        <h2>Archival information package <small><xsl:value-of select="premis:originalName"/></small></h2>
                    </xsl:if>

                    <xsl:if test="not ($object_id_type = 'preservation-aip-id' and /premis:premis/premis:event/premis:eventOutcomeInformation/premis:eventOutcome = 'failure')">
                        <table class="table table-condensed">
                            <tr class="row">
                                <th class="col-md-8">Event name</th>
                                <th class="col-md-2">Time</th>
                                <th class="col-md-2 text-right">Outcome</th>
                            </tr>

                            <xsl:for-each select="/premis:premis/premis:event/premis:linkingObjectIdentifier/premis:linkingObjectIdentifierValue[text() = $object_id_value]">
                                <!-- Filter out some events -->
                                <xsl:variable name="eventDetail" select="../../premis:eventDetail"/>
                                <xsl:if test="not(contains($eventDetail, 'Validation compilation of submission information package')) and not(contains($eventDetail, 'Transfer of submission information package'))">

                                    <xsl:choose>
                                        <xsl:when test="../../premis:eventOutcomeInformation/premis:eventOutcome = 'failure'">
                                            <tr class="red row">
                                                <td class="col-md-8"><xsl:value-of select="../../premis:eventDetail"/></td>
                                                <xsl:choose>
                                                    <xsl:when test="contains(../../premis:eventDateTime/text(), '.')">
                                                        <td class="col-md-2"><xsl:value-of select="substring-before(../../premis:eventDateTime/text(), '.')"/></td>
                                                    </xsl:when>
                                                    <xsl:otherwise>
                                                        <td class="col-md-2"><xsl:value-of select="../../premis:eventDateTime/text()"/></td>
                                                    </xsl:otherwise>
                                                </xsl:choose>
                                                <td class="col-md-2 text-right"><xsl:value-of select="../../premis:eventOutcomeInformation/premis:eventOutcome"/></td>
                                            </tr>
                                        </xsl:when>
                                        <xsl:otherwise>
                                            <tr class="success row">
                                                <td class="col-md-8"><xsl:value-of select="../../premis:eventDetail"/></td>
                                                <xsl:choose>
                                                    <xsl:when test="contains(../../premis:eventDateTime/text(), '.')">
                                                        <td class="col-md-2"><xsl:value-of select="substring-before(../../premis:eventDateTime/text(), '.')"/></td>
                                                    </xsl:when>
                                                    <xsl:otherwise>
                                                        <td class="col-md-2"><xsl:value-of select="../../premis:eventDateTime/text()"/></td>
                                                    </xsl:otherwise>
                                                </xsl:choose>
                                                <td class="col-md-2 text-right"><xsl:value-of select="../../premis:eventOutcomeInformation/premis:eventOutcome"/></td>
                                            </tr>
                                        </xsl:otherwise>
                                    </xsl:choose>
                                </xsl:if>
                            </xsl:for-each>
                        </table>
                    </xsl:if>
                </xsl:for-each>
            <br/>
            <hr/>

            <footer>
                Please note that this report is a summary of digitally signed and machine-readable report generated by the digital preservation service.
            </footer>

            </body>
        </html>
    </xsl:template>
</xsl:stylesheet>
