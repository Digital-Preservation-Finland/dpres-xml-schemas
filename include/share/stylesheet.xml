<?xml version="1.0"?>
<!-- This is XSLT stylesheet for converting Premis XML reports to more simplified HTML version.
     Usage: xsltproc stylesheet.xml report.xml  > report.html -->

<xsl:stylesheet version="1.0"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:premis="info:lc/xmlns/premis-v2">
    
    <xsl:template match="/">
        <html lang="en">
            <head>
                <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
                <style>
                    <xsl:value-of select="document('css/bootstrap.min.css')" disable-output-escaping="yes" />
                    <xsl:value-of select="document('css/bootstrap.css')" disable-output-escaping="yes" />
                    <xsl:value-of select="document('css/report.css')" disable-output-escaping="yes" />
                </style>

                <title>PAS – Inspection report</title>
            </head>
            <body>
                <div id="row">
                    <xsl:choose>
                        <xsl:when test="//premis:event/premis:eventOutcomeInformation/premis:eventOutcome = 'failure'">
                            <div class="col-md-10"><h1>Inspection report – <span class="text-red">Submission rejected</span></h1></div>
                        </xsl:when>
                        <xsl:otherwise>
                            <div class="col-md-10"><h1>Inspection report – <span class="text-success">Submission accepted</span></h1></div>
                        </xsl:otherwise>
                    </xsl:choose>
                    
                    <div class="col-md-2 clearfix"><img class="pull-right logo"  height="100px"/></div>
                </div>
                <div id="row">
                    <div class="col-md-12"><hr class="divider"/></div>
                </div>
                <div id="row">
                    <div class="col-md-10"/>
                    <div class="col-md-2">
                        <h5 class="text-right">
                            <small>
                                <xsl:value-of select="//premis:event/premis:eventType[text() = 'validation']/../premis:eventDateTime/text()"/>
                            </small>
                        </h5>
                    </div>
                </div>

                <xsl:choose>
                    <xsl:when test="//premis:event/premis:eventOutcomeInformation/premis:eventOutcome = 'failure'">
                        <xsl:variable name="objid" select="//premis:object/premis:environment/premis:dependency/premis:dependencyIdentifier/premis:dependencyIdentifierType[text() = 'mets:OBJID']/../premis:dependencyIdentifierValue/text()"/>
                        <xsl:variable name="username" select="//premis:agent/premis:agentIdentifier/premis:agentIdentifierType[text() = 'pas-user-id']/../../premis:agentName/text()"/>
                        
                        <p class="lead">Digital preservation system has received submission information package <strong><xsl:value-of select="$objid"/></strong> from user <strong><xsl:value-of select="$username"/></strong> and performed following actions. Some actions have failed and preservation responsibility has not been transferred to the digital preservation system.
                        </p>
                    </xsl:when>
                    <xsl:otherwise>
                        <xsl:variable name="objid" select="//premis:object/premis:environment/premis:dependency/premis:dependencyIdentifier/premis:dependencyIdentifierType[text() = 'mets:OBJID']/../premis:dependencyIdentifierValue/text()"/>
                        <xsl:variable name="username" select="//premis:agent/premis:agentIdentifier/premis:agentIdentifierType[text() = 'pas-user-id']/../../premis:agentName/text()"/>
                        
                        <p class="lead">Digital preservation system has received submission information package <strong><xsl:value-of select="$objid"/></strong> from user <strong><xsl:value-of select="$username"/></strong> and performed following actions. All actions were successful and preservation responsibility has been transferred to the digital preservation system.
                        </p>
                    </xsl:otherwise>
                </xsl:choose>
                

                
                <xsl:for-each select="//premis:object">
                    <xsl:variable name="object_id_type" select="premis:objectIdentifier/premis:objectIdentifierType"/>
                    <xsl:variable name="object_id_value" select="premis:objectIdentifier/premis:objectIdentifierValue"/>

                    <xsl:if test="$object_id_type = 'pas-transfer-id'">
                        <h2>Transfer <small><xsl:value-of select="premis:originalName"/></small></h2>
                    </xsl:if>
                    <xsl:if test="$object_id_type = 'pas-sip-id'">
                        <h2>Submission information package <small><xsl:value-of select="premis:originalName"/></small></h2>
                    </xsl:if>
                    <xsl:if test="$object_id_type = 'pas-sig-id'">
                        <h2>Digital signature <small><xsl:value-of select="premis:originalName"/></small></h2>
                    </xsl:if>
                    <xsl:if test="$object_id_type = 'pas-mets-id'">
                        <h2>Metadata object <small><xsl:value-of select="premis:originalName"/></small></h2>
                    </xsl:if>
                    <xsl:if test="$object_id_type = 'pas-object-id'">
                        <h2>Digital object <small><xsl:value-of select="premis:originalName"/></small></h2>
                    </xsl:if>
                    <xsl:if test="$object_id_type = 'pas-aip-id'">
                        <h2>Archival information package <small><xsl:value-of select="premis:originalName"/></small></h2>
                    </xsl:if>
                    
                    <table class="table table-condensed">
                        <tr class="row">
                            <th class="col-md-8">Event name</th>
                            <th class="col-md-4">Outcome</th>
                        </tr>
                        
                        <xsl:for-each select="//premis:event/premis:linkingObjectIdentifier/premis:linkingObjectIdentifierValue[text() = $object_id_value]">
                            <!-- Filter out some events -->
                            <xsl:variable name="eventDetail" select="../../premis:eventDetail"/>
                            <xsl:if test="not(contains($eventDetail, 'Validation compilation of a SIP')) and not(contains($eventDetail, 'Ingestion of the transfer'))">

                                <xsl:choose>
                                    <xsl:when test="../../premis:eventOutcomeInformation/premis:eventOutcome = 'failure'">
                                        <tr class="red row">
                                            <td class="col-md-8"><xsl:value-of select="../../premis:eventDetail"/></td>
                                            <td class="col-md-4"><xsl:value-of select="../../premis:eventOutcomeInformation/premis:eventOutcome"/></td>
                                        </tr>
                                    </xsl:when>
                                    <xsl:otherwise>
                                        <tr class="success row">
                                            <td class="col-md-8"><xsl:value-of select="../../premis:eventDetail"/></td>
                                            <td class="col-md-4"><xsl:value-of select="../../premis:eventOutcomeInformation/premis:eventOutcome"/></td>
                                        </tr>
                                    </xsl:otherwise>
                                </xsl:choose>
                            </xsl:if>
                        </xsl:for-each>
                    </table>
                </xsl:for-each>
            <br/>
            <hr/>
            
            <center>
            <h6>Please note that this report is a summary of digitally signed and machine-readable report generated by digital preservation system.</h6>
            </center>

            </body>
        </html>
    </xsl:template>
</xsl:stylesheet>
